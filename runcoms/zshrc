#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

setopt nonomatch
typeset  -U path cdpath fpath manpath

## path
path=(
       $JAVA_HOME/bin(N-/) \
       /usr/local/bin(N-/) \
       $HOME/bin(N-/) \
       $HOME/.cabal/bin(N-/) \
       $HOME/.vim/bundle/powerline/scripts(N-/) \
       $path \
)
export path

# fpath
fpath=(/usr/local/share/zsh-completions(N-/) \
  /usr/local/share/zsh/site-functions(N-/) \
  $fpath \
)
autoload -U compinit
compinit -u

###############################
# Alias configuration
#  expand aliases before completing
###############################
alias ls='ls -GFv'
alias lsal='ls -al'

###############################
# path priority
###############################
alias mvn='/usr/local/bin/mvn'

###############################
# cover mistype
###############################
alias ,,='cd ../'
alias //_='cd ../'
alias ...='cd ../'

# aliases for all shells
alias cp='cp -i'
alias df='df -h'
alias dir='ls -l'
alias du='du -h'
alias grep='grep --color'
alias j="jobs -l"
alias jobs='jobs -l'
alias mv='mv -i'
alias rm='rm -i'
alias whence='type -a'
alias where="command -v"
alias diff='colordiff'
# global aliases
alias -g L='|less'
alias -g H='|head'
alias -g T='|tail'
alias -g G='|grep'
alias -g V='|vim -R -'
alias -g X='|xargs'

#=============================
# utlity
#==============================
function exists() { which $1 &> /dev/null }

#=============
# environment
#=============
export PAGER='less'
export LESS='-R -X -i --no-init --LONG-PROMPT --ignore-case'
export LESSOPEN='| src-hilite-lesspipe.sh %s'
export LV='-la -Ou8 -c'
export JAVA_HOME=`/usr/libexec/java_home`
export LANG=ja_JP.UTF-8
export RBENV_ROOT=/usr/local/opt/rbenv
export EDITOR="vim"

if exists go; then
  export GOROOT=`go env GOROOT`
  export GOPATH=$HOME/.go
  #export PATH=$PATH:$GOROOT/bin:$GOPATH/bin
fi


# 3秒以上かかった処理は詳細表示
REPORTTIME=3

#=============================
# source z.sh
#=============================
if exists brew; then  . `brew --prefix`/etc/profile.d/z.sh; fi

#=============================
# direnv
#=============================
if exists direnv; then eval "$(direnv hook zsh)"; fi

#=============================#
# command-line stack
#=============================#
show_buffer_stack() {
  POSTDISPLAY="
stack: $LBUFFER"
  zle push-line-or-edit
}
zle -N show_buffer_stack

if exists rbenv; then
    eval "$(rbenv init - zsh)";
    . $RBENV_ROOT/completions/rbenv.zsh
fi


function do_enter() {
    if [ -n "$BUFFER" ]; then
        zle accept-line
        return 0
    fi
    echo
    ls
    # ↓おすすめ
    # ls_abbrev
    if [ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" = 'true' ]; then
        echo
        echo -e "\e[0;33m--- git status ---\e[0m"
        git status -sb
    fi
    zle reset-prompt
    return 0
}
zle -N do_enter
bindkey '^m' do_enter


#===========================#
# percol
#===========================#

if exists percol; then
    function percol_select_history() {
        local tac
        exists gtac && tac="gtac" || { exists tac && tac="tac" || { tac="tail -r" } }
        BUFFER=$(history -n 1 | eval $tac | percol --query "$LBUFFER")
        CURSOR=$#BUFFER         # move cursor
        zle -R -c               # refresh
    }

    zle -N percol_select_history
    bindkey '^R' percol_select_history
fi
zle -N percol-select-history

#===========================#
# peco
#===========================#
function peco-select-history() {
    local tac
    if exists tac;  then
        tac="tac"
    else
        tac="tail -r"
    fi
    BUFFER=$(history -n 1 | \
        eval $tac | \
        peco --query "$LBUFFER")
    CURSOR=$#BUFFER
   # zle clear-screen
}
zle -N peco-select-history
bindkey '^r' peco-select-history

function peco-src () {
    local selected_dir=$(ghq list --full-path | peco --query "$LBUFFER")
    if [ -n "$selected_dir" ]; then
        BUFFER="cd ${selected_dir}"
        zle accept-line
    fi
    zle clear-screen
}
zle -N peco-src
bindkey '^S' peco-src

function peco-godoc() {
    local selected_dir=$(ghq list --full-path | peco --query "$LBUFFER")
    if [ -n "$selected_dir" ]; then
        BUFFER="godoc ${selected_dir} | less"
        zle accept-line
    fi
    zle clear-screen
}

zle -N peco-godoc

#===========================#
# tmux session
#===========================#
[[ -z "$TMUX" && ! -z "$PS1" ]] && tmux
